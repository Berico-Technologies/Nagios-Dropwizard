apply plugin: 'java'
apply plugin: 'maven'

ext {
    dropwizardVersion = "0.6.2"
}

configurations {
    integTest.extendsFrom testRuntime
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

sourceSets {
    // Note that just declaring this sourceset creates two configurations.
    integTest {
        java.srcDir file('src/integration-test/java')
        resources.srcDir file('src/integration-test/resources')
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.integTest
        runtimeClasspath = output + sourceSets.test.output + compileClasspath
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile "com.yammer.dropwizard:dropwizard-core:${dropwizardVersion}"
    compile "commons-lang:commons-lang:2.6"
    testCompile "junit:junit:4.11"
    testCompile "org.mockito:mockito-all:1.9.5"
    testCompile "com.yammer.dropwizard:dropwizard-testing:${dropwizardVersion}"
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "http://nexus.berico.us/content/repositories/releases/") {
                authentication(userName: berico_nexusUsername, password: berico_nexusPassword)
            }
            snapshotRepository(url: "http://nexus.berico.us/content/repositories/snapshots/") {
                authentication(userName: berico_nexusUsername, password: berico_nexusPassword)
            }
            pom.groupId = "com.bericotech"
            pom.artifactId = "dropwizard-nagios"
        }
    }
}

task integTest(type:Test){
    description = "Run integration tests (located in src/integration-test/...)."
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}
